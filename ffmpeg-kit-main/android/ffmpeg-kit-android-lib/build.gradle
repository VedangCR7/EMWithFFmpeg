apply plugin: 'com.android.library'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.arthenica.ffmpegkit'
    }
    compileSdk 33
    ndkVersion "22.1.7171670"

    defaultConfig {
        minSdk 24
        targetSdk 33
        versionCode 240600
        versionName "6.0"
        project.archivesBaseName = "ffmpeg-kit"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            // Point to your custom FFmpeg 6.1.1 libraries
            jniLibs.srcDirs = ["../../android/app/src/main/jniLibs"]
        }
    }

    // Packaging options to prevent ffmpeg-kit from packaging its own .so files
    packagingOptions {
        exclude '**/*.so'
        // Allow only your custom libraries
        pickFirst '**/libavcodec.so'
        pickFirst '**/libavfilter.so'
        pickFirst '**/libavformat.so'
        pickFirst '**/libavutil.so'
        pickFirst '**/libpostproc.so'
        pickFirst '**/libswresample.so'
        pickFirst '**/libswscale.so'
        pickFirst '**/libavdevice.so'
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            systemProperty 'enable.ffmpeg.kit.test.mode', 'true'
        }
    }

    publishing {
        singleVariant('release') {
            withJavadocJar()
            withSourcesJar()
        }
    }
}

task javadoc(type: Javadoc) {
    title = 'FFmpegKit'
    destinationDir = file("${projectDir}/../../docs/android/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    source = android.sourceSets.main.java.srcDirs
    configurations.implementation.setCanBeResolved(true)
    classpath += configurations.implementation
}

dependencies {
    api 'com.arthenica:smart-exception-java:0.2.1'
    testImplementation "androidx.test.ext:junit:1.1.5"
    testImplementation 'org.json:json:20230618'
}
    